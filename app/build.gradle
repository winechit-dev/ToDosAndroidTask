plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    namespace 'com.wcp.todosandroidtask'
    compileSdk 33

    defaultConfig {
        applicationId "com.wcp.todosandroidtask"
        minSdk 23
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("../keystore/release.keystore")
            storePassword RELEASE_KEY_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
        debug {
            storeFile file('../keystore/debug.keystore')
            storePassword DEBUG_KEY_STORE_PASSWORD
            keyAlias DEBUG_KEY_ALIAS
            keyPassword DEBUG_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "stage"

    productFlavors {
        dev {
            dimension "stage"
            versionNameSuffix "-dev"
            applicationIdSuffix '.dev'
            resValue "string", "app_name", "Todoist Dev"
        }

        uat {
            dimension "stage"
            versionNameSuffix "-uat"
            applicationIdSuffix '.uat'
            resValue "string", "app_name", "Todoist Uat"
        }

        prod {
            dimension "stage"
            resValue "string", "app_name", "Todoist"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    // Allow references to generated code
    kapt {
        correctErrorTypes true
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation project(':domain')
    implementation project(':data')

    implementation "androidx.core:core-ktx:$versions.core_ktx"
    implementation "androidx.appcompat:appcompat:$versions.appcompat"
    implementation "com.google.android.material:material:$versions.material"
    implementation "androidx.constraintlayout:constraintlayout:$versions.constraintLayout"

    implementation "androidx.activity:activity-ktx:$versions.activity_ktx"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle"

    // dagger
    implementation "com.google.dagger:hilt-android:$versions.dagger"
    kapt "com.google.dagger:hilt-android-compiler:$versions.dagger"
    kapt "androidx.hilt:hilt-compiler:$versions.dagger_lifecycle"

    // room database
    implementation "androidx.room:room-runtime:$versions.room"
    kapt "androidx.room:room-compiler:$versions.room"

    testImplementation "junit:junit:$versions.junit"
    androidTestImplementation "androidx.test.ext:junit:$versions.junitExt"
    androidTestImplementation "androidx.test.espresso:espresso-core:$versions.espresso"
}